#version 430

#define PI 3.1415926535897932384626433832795

layout (location = 0, rgba16f) uniform image2D map;
layout (location = 1) uniform uint seed;
layout (location = 2) uniform float delta;

layout (location = 3) uniform float speed;
layout (location = 4) uniform float turnSpeed;
layout (location = 5) uniform float sensorAngle;
layout (location = 6) uniform float sensorDistance;
layout (location = 7) uniform int sensorSize;

struct Agent
{
	vec2 position;
	float angle;
	int id;
	//vec2 direction;
};

layout(std430, binding = 0) buffer ssbo
{
	Agent agents[];
};

uint hash(uint state);
float random(uint state);
float sense(const Agent agent, float angleOff);

layout (local_size_x = 4, local_size_y = 4) in;

const ivec2 imgSize = imageSize(map);
const uint id = gl_GlobalInvocationID.y * (gl_NumWorkGroups.x * gl_WorkGroupSize.x) + gl_GlobalInvocationID.x;

void main() 
{
	const Agent agent = agents[id];
	const uint uniqueState = uint(agent.position.y * imgSize.x + agent.position.x) + hash(id);

	// Turn weights
	const float wForward = sense(agent, 0.0);
	const float wLeft = sense(agent, sensorAngle);
	const float wRight = sense(agent, -sensorAngle);
	const float rng = random(uniqueState);
	float turnStr = rng * turnSpeed * delta;

	if (wForward > wLeft && wForward > wRight)
	{	// Stay oncourse
		agents[id].angle += 0.0;
	}
	else if (wRight - wLeft > .01)
	{	// Go right
		agents[id].angle -= turnStr;
	}
	else if (wLeft - wRight > .01)
	{	// Go left
		agents[id].angle += turnStr;
	}
	else
	{	// Go in random direction
		agents[id].angle += 2 * turnStr - turnSpeed * delta;
	}

	// Get new direction
	vec2 direction = vec2(cos(agents[id].angle), sin(agents[id].angle));
	// Get new position
	vec2 pos = agent.position + direction * delta * speed;

	// Out of bounds, get new direction for next cycle
	if (pos.x < 0.0 || pos.x >= imgSize.x || pos.y < 0.0 || pos.y >= imgSize.y)
	{	// Set position back
		pos = agent.position;
		agents[id].angle = rng * 2 * PI;
	}

	agents[id].position = pos;
	imageStore(map, ivec2(pos), vec4(1.0));
	imageStore(map, ivec2(pos - direction), vec4(1.0));
	imageStore(map, ivec2(pos - direction * 2), vec4(1.0));
}

float sense(const Agent agent, float angleOff)
{
	const vec2 sensorDir = vec2(cos(agent.angle + angleOff), sin(agent.angle + angleOff));
	const ivec2 sensorPos = ivec2(agent.position + sensorDistance * sensorDir);

	float weight = 0.0;
	for (int i = -sensorSize; i <= sensorSize; i++)
	{
		for (int j = -sensorSize; j <= sensorSize; j++)
		{
			const ivec2 ij = sensorPos + ivec2(i, j);
			if (ij.x >= 0 || ij.x < imgSize.x || ij.y >= 0 || ij.y < imgSize.y)
			{
				const vec4 clr = imageLoad(map, ij);
				weight += (clr.r + clr.g + clr.b + clr.a) / 4.0;
			}
		}
	}
	return weight;
}

float random(uint state)
{
	return float(hash(state)) / 4294867295.0;
}

uint hash(uint state)
{
	state *= seed;
	state ^= 2747636419u;
	state *= 2654435769u;
	state ^= state >> 16;
	state *= 2654435769u;
	state ^= state >> 16;
	state *= 2654435769u;
	return state;
}